plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'maven-publish'
}

group = 'arte.programar'
version = '1.0'

// Use ./gradlew bintrayUpload to upload
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['release']

    pkg {
        repo = 'Android-MaterialFilePicker'
        name = 'arte.programar.materialfile'
        licenses = ["Apache-2.0"]
        vcsUrl = 'https://github.com/arteaprogramar/Android-MaterialFilePicker.git'
        websiteUrl = 'https://github.com/arteaprogramar/Android-MaterialFilePicker'

        version {
            name = project.version
        }
    }

}

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    scm {
        url 'https://github.com/arteaprogramar/Android-MaterialFilePicker'
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact sourceJar
                groupId group
                artifactId 'materialfile'
                version version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'SNew Material File Picker 2020.')
                    root.appendNode('name', 'MaterialFile')
                    root.appendNode('url', 'https://github.com/arteaprogramar/Android-MaterialFilePicker')
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'com.google.android.material:material:1.3.0-alpha02'
}